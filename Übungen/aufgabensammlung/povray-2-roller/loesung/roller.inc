// Datei:  roller.inc
// Autor:  Sebastian Birk, Holger Arndt
// Datum:  08.01.2016

#include "colors.inc"
#include "shapes.inc"



// #######################
// Makros
#macro Roller( TexturGummi,        //
               TexturChrom,        //
               TexturRahmen,       //
               TexturTrittbrett,   //
               LenkerDrehung       // Lenker Drehung
             )
#local Speiche = cylinder {
  -14*y,
  14*y,
  .2
  texture {TexturChrom}
}

#local Achse = cylinder {
  -5*z,
  5*z,
  1
  texture {TexturChrom}
}

#local Felge = difference {
  cylinder { -1*z, 1*z, 11.5 }
  cylinder { -2*z, 2*z, 10 }
  texture {TexturChrom}
}

#local AnzSpeichen = 10;
#local Speichen = union {
  #local CountSpeiche = 0;
  #while (CountSpeiche < AnzSpeichen)
    object { Speiche
      rotate (CountSpeiche*(180/AnzSpeichen))*z
    }
    #local CountSpeiche = CountSpeiche + 1;
  #end
}

#local Reifen = torus {
  14, 3
  texture { TexturGummi }
  rotate 90*x
}

#local Rad = union {
  object { Speichen }
  object { Achse }
  object { Reifen }
  object { Felge }
}

#local Biegung = difference {
  torus {
    5, 1
    rotate 90*x
  }
  box { <-7,-7,-2>, <0,7,2> }
  box { <-7,-7,-2>, <7,0,2> }
  texture { TexturRahmen }
}

#local HalbGabel = union {
  object { Biegung }
  cylinder {
    <5,0,0>, <5,-15,0>, 1
  }
  sphere { <5,-15,0>, 1 }
  texture { TexturRahmen }
}

#local Gabel = union {
  object { HalbGabel }
  object {
    HalbGabel
    rotate 180*y
  }
}

#local Griff = union {
  cylinder { <0,0,0>, <0,0,.5>, 2 }
  cylinder { <0,0,.5>, <0,0,10.5>, 1.5 }
  cylinder { <0,0,10.5>, <0,0,11>, 2 }
 texture { TexturGummi }
}

#local Lenker = union {
//<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
  object {
    Gabel
    translate 15*y
    rotate 90*y
  }
  object {
    Rad
  }
  cylinder { // Lenkerstange
    <0,18,0>, <0,64,0>, 1.5
    texture { TexturRahmen }
  }
  cylinder { // Griffstange
    <0,64,-15>, <0,64,15>, 1.5
    texture { TexturRahmen }
  }
  object { // Griff rechts
    Griff
    translate 15*z
    translate 64*y
  }
  object { // Griff links
    Griff
    translate 15*z
    rotate 180*x
    translate 64*y
  }
  cylinder { // Gelenk
    22*y, 26*y, 2
    texture { TexturRahmen }
  }
//>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
}

// Verbindung Lenker-Trittbrett
#local Verbindung = difference {
  torus {
    24, 2
    rotate 90*x
    texture { TexturRahmen }
  }
  box {
    <-30,-30,-3>, <30,0,3>
  }
  box {
    <-30,-30,-3>, <0,30,3>
    rotate -20*z
  }
}

// Trittbrett
//<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
#local Trittbrett = difference {
  box { <5,0,-12>, <45,.5,12> texture { TexturTrittbrett } }
  box { <-10,-1,-20>, <0,1,20> rotate 45*y }
  box { <-10,-1,-20>, <0,1,20> rotate -45*y }
}
//>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

// Rahmen 
//<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
#local Rahmen = union {
  cylinder { <0,0,0>, <10,0,10>, 1.5  }
  cylinder { <0,0,0>, <10,0,-10>, 1.5  }
  sphere { <10,0,10>, 1.5 }
  sphere { <10,0,-10>, 1.5 }
  cylinder { <10,0,10>, <45,0,10>, 1.5  }
  cylinder { <10,0,-10>, <45,0,-10>, 1.5  }
  sphere { <45,0,10>, 1.5 }
  sphere { <45,0,-10>, 1.5 }
  cone { <45,0,10>, 1.5, <63,-1.5,5>, 1  }
  cone { <45,0,-10>, 1.5,  <63,-1.5,-5>, 1  }
  sphere { <63,-1.5,5>, 1 }
  sphere { <63,-1.5,-5>, 1 }
  texture { TexturRahmen }
}
//>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>

union { // Roller
//<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
  object {
    Lenker
    rotate LenkerDrehung*y
    rotate -20*z
  }
  object {
    Verbindung
  }
  object {
    Trittbrett
    translate 21*x
    translate 3*y
  }
  object {
    Rahmen
    translate 23.5*x
    translate 1.5*y
  }
  object {
    Rad
    translate (63+23.5)*x
  }
  translate 17*y
//>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>
}

#end //------------------- end of macro







